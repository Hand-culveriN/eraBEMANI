
;;;TARGETへの代入等は関数呼び出し前後で行う
@EXECTRAIN_SCHOOL, シチュエーション
#DIMS DYNAMIC シチュエーション
#DIMS DYNAMIC 場所名
#DIMS DYNAMIC 行為前引数
#DIMS DYNAMIC 行為後引数
#DIM DYNAMIC コンドーム相手持ち
#DIM DYNAMIC コンドーム行為前所持数
#DIM DYNAMIC コンドーム買わない率
場所名 = %シチュエーション%
行為前引数 = %シチュエーション%_行為前
行為後引数 = %シチュエーション%_行為後

SELECTCASE シチュエーション
	CASE "トイレ"
		トイレ使用中 = 1
	CASE "青姦"
		野外プレイ中 = 1
	;場所名変更系
	CASE "宿泊イベント"
		場所名 = 自宅
		行為前引数 = 自宅に宿泊
		行為後引数 = 宿泊_行為後
	CASE "自宅イベント"
		場所名 = 自宅
		行為前引数 = 
		行為後引数 = 自宅_行為後
	CASE "保健室ベッドシェア", "保健室教師"
		場所名 = 保健室
		行為前引数 = 
		行為後引数 = 保健室_行為後
	CASE "保健室眠姦"
		場所名 = 保健室
		行為前引数 = 
		行為後引数 = 
	CASE "登校前ホテル", "ホテル朝プレイ"
		場所名 = ホテル
ENDSELECT

SIF 行為前引数 != ""
	CALL SCHOOL_KOJO, TARGET, 行為前引数

コンドーム相手持ち = 0
IF 場所名 == "ホテル" && !ホテル入室済み
	IF PENIS(MASTER) || PENIS(TARGET)
		IF PENIS(MASTER)
			IF VAGINA(TARGET)
				コンドーム買わない率 += 中出され回数:NOF(MASTER)
			ELSE
				コンドーム買わない率 += アナル中出され回数:NOF(MASTER)
			ENDIF
		ENDIF
		IF PENIS(TARGET)
			IF VAGINA(MASTER)
				コンドーム買わない率 += 中出され回数:MASTER:NOF(TARGET)
			ELSE
				コンドーム買わない率 += アナル中出され回数:MASTER:NOF(TARGET)
			ENDIF
		ENDIF
		IF (CSTR:在任 != "" || TALENT:恋慕) && !RANDPER(コンドーム買わない率)
			コンドーム相手持ち = 12
			PRINTFORML %CALLNAME:TARGET%はホテルへの道中でコンドームを1箱買っていくようだ
			SIF MONEY >= 500
				PRINTFORML %CALLNAME:MASTER%も買っていきますか？
		ELSE
			SIF MONEY >= 500
				PRINTL ホテルへの道中でコンドームを買っていきますか？
		ENDIF
		IF MONEY >= 500
			PRINTL [0] 買わない
			PRINTL [1] 1箱(12個)買う
			SIF MONEY >= 1000
				PRINTL [2] 2箱(24個)買う
			SIF MONEY >= 1500
				PRINTL [3] 3箱(36個)買う
			BINPUT
			IF RESULT
				MONEY -= RESULT*500
				ITEM:コンドーム += RESULT*12
				PRINTFORMW {RESULT}箱購入しました
			ENDIF
		ENDIF
	ENDIF
	PRINTW ホテルに入り、ローションとコンドームを手に入れた
	ITEM:ローション++
	ITEM:コンドーム++
ENDIF

SIF 場所名 == "ホテル"
	ITEM:プレイマット = 1

IF コンドーム相手持ち
	コンドーム行為前所持数 = ITEM:コンドーム
	ITEM:コンドーム += コンドーム相手持ち
ENDIF

CALL CHECKDRAWLINE
CALL TRAIN_SCHOOL, 場所名
CALL CHECKDRAWLINE

IF コンドーム相手持ち 
	SIF ITEM:コンドーム > コンドーム行為前所持数
		ITEM:コンドーム = コンドーム行為前所持数
ENDIF
ITEM:プレイマット = 0

SIF 行為後引数 != ""
	CALL SCHOOL_KOJO, TARGET, 行為後引数

;同じ学校の教師と生徒の場合、無条件でインモラル+1
IF CSTR:在任 == CSTR:MASTER:在校
	CFLAG:インモラル++
	CFLAG:MASTER:インモラル++
ENDIF

;シチュエーション別の上昇処理
SELECTCASE シチュエーション
	CASE "自宅"
		IF TALENT:恋慕
			CALL 好感度上昇, TARGET, "友情", 20
			IF 好感度:既成事実
				CALL 好感度上昇, TARGET, "ときめき", ときめき増加量*20
			ELSE
				ときめき増加量 += 3
				CALL 好感度上昇, TARGET, "ときめき", ときめき増加量*25
			ENDIF
			CALL 好感度上昇, TARGET, "セフレ", 30
			CALL 好感度上昇, TARGET, "既成事実", 1
		ELSE
			CALL 好感度上昇, TARGET, "友情", 10
			CALL 好感度上昇, TARGET, "ときめき", ときめき増加量*10
			CALL 好感度上昇, TARGET, "セフレ", 60
			CALL 好感度上昇, TARGET, "既成事実", 1
			CFLAG:インモラル++
			CFLAG:MASTER:インモラル++
		ENDIF
	CASE "ホテル", "ホテル朝プレイ"
		IF TALENT:恋慕
			CALL 好感度上昇, TARGET, "友情", 60
			IF 好感度:既成事実
				CALL 好感度上昇, TARGET, "ときめき", ときめき増加量*30
			ELSE
				ときめき増加量 += 5
				CALL 好感度上昇, TARGET, "ときめき", ときめき増加量*35
			ENDIF
			CALL 好感度上昇, TARGET, "セフレ", 50
			CALL 好感度上昇, TARGET, "既成事実", 1
		ELSE
			CALL 好感度上昇, TARGET, "友情", 15
			CALL 好感度上昇, TARGET, "ときめき", ときめき増加量*10
			CALL 好感度上昇, TARGET, "セフレ", 100
			CALL 好感度上昇, TARGET, "既成事実", 1
			CFLAG:インモラル += 3
			CFLAG:MASTER:インモラル += 3
		ENDIF
	CASE "登校前ホテル"
		IF TALENT:同行者:恋慕
			CALL 好感度上昇, 同行者, "友情", 5
			CALL 好感度上昇, 同行者, "ときめき", ときめき増加量*15
			IF CSTR:同行者:在任 == CSTR:PLAYER:在校
				CALL 好感度上昇, 同行者, "セフレ", 90
			ELSE
				CALL 好感度上昇, 同行者, "セフレ", 60
			ENDIF
		ELSE
			CALL 好感度上昇, 同行者, "友情", 5
			CALL 好感度上昇, 同行者, "ときめき", ときめき増加量*5
			IF CSTR:同行者:在任 == CSTR:PLAYER:在校
				CALL 好感度上昇, 同行者, "セフレ", 300
			ELSE
				CALL 好感度上昇, 同行者, "セフレ", 150
			ENDIF
			CFLAG:インモラル++
			CFLAG:MASTER:インモラル++
		ENDIF
	CASE "トイレ", "青姦"
		CALL 好感度上昇, TARGET, "友情", 30
		CALL 好感度上昇, TARGET, "ときめき", ときめき増加量*5
		CALL 好感度上昇, TARGET, "セフレ", 80
		CALL 好感度上昇, TARGET, "既成事実", 1
		IF !TALENT:恋慕
			CFLAG:インモラル += 4
			CFLAG:MASTER:インモラル += 4
		ELSE
			CFLAG:インモラル += 2
			CFLAG:MASTER:インモラル += 2
		ENDIF
	CASE "宿泊イベント"
		IF 好感度:ときめき >= 1000 && 好感度:ときめき > 好感度:セフレ && !TALENT:恋慕
			CALL 恋仲, TARGET
			WAIT
		ENDIF
		IF TALENT:恋慕
			CALL 好感度上昇, TARGET, "友情", 30
			IF 好感度:既成事実
				CALL 好感度上昇, TARGET, "ときめき", ときめき増加量*25
			ELSE
				ときめき増加量 += 5
				CALL 好感度上昇, TARGET, "ときめき", ときめき増加量*30
			ENDIF
			CALL 好感度上昇, TARGET, "セフレ", 40
			CALL 好感度上昇, TARGET, "既成事実", 1
		ELSE
			CALL 好感度上昇, TARGET, "友情", 15
			CALL 好感度上昇, TARGET, "ときめき", ときめき増加量*10
			CALL 好感度上昇, TARGET, "セフレ", 80
			CALL 好感度上昇, TARGET, "既成事実", 1
			CFLAG:インモラル++
			CFLAG:MASTER:インモラル++
		ENDIF
	CASE "自宅イベント"
		IF TALENT:恋慕
			CALL 好感度上昇, TARGET, "友情", 30
			IF 好感度:既成事実
				CALL 好感度上昇, TARGET, "ときめき", ときめき増加量*15
			ELSE
				ときめき増加量 += 3
				CALL 好感度上昇, TARGET, "ときめき", ときめき増加量*20
			ENDIF
			CALL 好感度上昇, TARGET, "セフレ", 25
			CALL 好感度上昇, TARGET, "既成事実", 1
		ELSE
			CALL 好感度上昇, TARGET, "友情", 15
			CALL 好感度上昇, TARGET, "ときめき", ときめき増加量*10
			CALL 好感度上昇, TARGET, "セフレ", 50
			CALL 好感度上昇, TARGET, "既成事実", 1
			CFLAG:インモラル++
			CFLAG:MASTER:インモラル++
		ENDIF
	CASE "保健室ベッドシェア"
		CALL 好感度上昇, TARGET, "友情", 30
		CALL 好感度上昇, TARGET, "ときめき", ときめき増加量*5
		CALL 好感度上昇, TARGET, "セフレ", 50
		CALL 好感度上昇, TARGET, "既成事実", 1
		IF !TALENT:恋慕
			CFLAG:インモラル += 3
			CFLAG:MASTER:インモラル += 3
		ELSE
			CFLAG:インモラル += 1
			CFLAG:MASTER:インモラル += 1
		ENDIF
	CASE "保健室", "保健室教師", "体育倉庫"
		CALL 好感度上昇, TARGET, "友情", 30
		CALL 好感度上昇, TARGET, "ときめき", ときめき増加量*5
		CALL 好感度上昇, TARGET, "セフレ", 50
		CALL 好感度上昇, TARGET, "既成事実", 1
		IF !TALENT:恋慕
			CFLAG:インモラル += 4
			CFLAG:MASTER:インモラル += 4
		ELSE
			CFLAG:インモラル += 2
			CFLAG:MASTER:インモラル += 2
		ENDIF
	CASE "校長室", "理事長室"
		CALL 好感度上昇, TARGET, "友情", 30
		CALL 好感度上昇, TARGET, "ときめき", ときめき増加量*5
		CALL 好感度上昇, TARGET, "セフレ", 50
		CALL 好感度上昇, TARGET, "既成事実", 1
		IF !TALENT:恋慕
			CFLAG:インモラル += 6
			CFLAG:MASTER:インモラル += 6
		ELSE
			CFLAG:インモラル += 3
			CFLAG:MASTER:インモラル += 3
		ENDIF
ENDSELECT
トイレ使用中 = 0
野外プレイ中 = 0
ホテル入室済み = 0

;フラグリセット
VARSET PREG
VARSET TEQUIP
VARSET TEQUIP:MASTER:0
学園モード時限イベント = 0
時限イベント内容 = 


@TRAIN_SCHOOL, 場所
#DIMS DYNAMIC 場所
#DIM DYNAMIC LCOUNT

CALL SCHOOL_COMFILTER, 場所

BASE:体力 = MAXBASE:体力
BASE:気力 = MAXBASE:気力

行為場所 = %場所%

CFLAG:インモラル = LIMIT(CFLAG:インモラル, -100, 100)

CALL EVENTUSERTRAIN
DO
	CALL SHOW_STATUS
	CALL SHOW_USERCOM
	;INPUT
	;CALL USERCOM
LOOP !TFLAG:調教終了

DRAWLINE

;珠の獲得（快と習得だけ加算される）
CALL JUEL_CHECK, 1

;能力の自動上昇処理
CALL SCHOOL_ABLUP, TARGET, 0

;イった回数に応じてときめき増加
ときめき増加量 = SUMARRAY(EX:TARGET:0)

;立つ鳥跡を濁さず
CALL アイテム修理
;口上で参照したいのでちょっと外してみる
;CALL CVAR_RESET

@SCHOOL_ABLUP, 対象, MODE
#DIM DYNAMIC 対象
#DIM DYNAMIC MODE
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 目標値
#DIM DYNAMIC 上昇済
#DIM DYNAMIC 対応珠
#DIM DYNAMIC 計算用EXP

;好感度上昇に付随する能力上昇はこっち
IF MODE == 1
	FOR LCOUNT, 0, VARSIZE("ABL")
		上昇済 = 0
		SIF ABL:対象:LCOUNT >= 10
			CONTINUE
		SELECTCASE ABLNAME:LCOUNT
			CASE "欲望"
				計算用EXP = 好感度:対象:友情+好感度:対象:セフレ*2+好感度:対象:既成事実*100
				DO
					SELECTCASE ABL:対象:欲望
						CASE 0
							目標値 = 300
						CASE 1
							目標値 = 800
						CASE 2
							目標値 = 1200
						CASE 3
							目標値 = 1800
						CASE 4
							目標値 = 3000
						CASE 5
							目標値 = 4200
						CASE 6
							目標値 = 5600
						CASE 7
							目標値 = 7000
						CASE 8
							目標値 = 8500
						CASE 9
							目標値 = 10000
						CASEELSE
							BREAK
					ENDSELECT
					SIF 計算用EXP < 目標値
						BREAK
					ABL:対象:欲望++
					上昇済++
				LOOP 1
			CASE "レズっ気"
				SIF SEX(対象) == 2 && SEX(PLAYER) == 2
					GOTO っ気の上昇
			CASE "ホモっ気"
				SIF SEX(対象) == 1 && SEX(PLAYER) == 1
					GOTO っ気の上昇
			CASE "っ気"
				$っ気の上昇
				;一旦目標値は欲望と同値でときめきをやや強く 要調整
				計算用EXP = 好感度:対象:友情+好感度:対象:ときめき*3+好感度:対象:既成事実*80
				DO
					SELECTCASE ABL:対象:LCOUNT
						CASE 0
							目標値 = 300
						CASE 1
							目標値 = 800
						CASE 2
							目標値 = 1200
						CASE 3
							目標値 = 1800
						CASE 4
							目標値 = 3000
						CASE 5
							目標値 = 4200
						CASE 6
							目標値 = 5600
						CASE 7
							目標値 = 7000
						CASE 8
							目標値 = 8500
						CASE 9
							目標値 = 10000
						CASEELSE
							BREAK
					ENDSELECT
					SIF 計算用EXP < 目標値
						BREAK
					ABL:対象:LCOUNT++
					上昇済 = 1
				LOOP 1
		ENDSELECT
		SIF ABL:対象:LCOUNT > 10
			ABL:対象:LCOUNT = 10
		SIF 上昇済
			PRINTFORML %NAME:対象%の%ABLNAME:LCOUNT%が{ABL:対象:LCOUNT}になった
	NEXT
	RETURN
ENDIF

FOR LCOUNT, 0, VARSIZE("ABL")
	上昇済 = 0
	SIF ABL:対象:LCOUNT >= 10
		CONTINUE
	SELECTCASE ABLNAME:LCOUNT
		CASE "Ｃ感覚"
			対応珠 = GETPALAM("快Ｃ")
			GOTO UPWITHJUEL感覚
		CASE "Ｖ感覚"
			対応珠 = GETPALAM("快Ｖ")
			GOTO UPWITHJUEL感覚
		CASE "Ａ感覚"
			対応珠 = GETPALAM("快Ａ")
			GOTO UPWITHJUEL感覚
		CASE "Ｂ感覚"
			対応珠 = GETPALAM("快Ｂ")
			GOTO UPWITHJUEL感覚
		CASE "技巧"
			対応珠 = GETPALAM("習得")
			GOTO UPWITHJUEL
		CASE "ゲイ中毒"
			IF EXP:対象:ゲイ経験/300 > ABL:対象:ゲイ中毒
				ABL:対象:ゲイ中毒 = EXP:対象:ゲイ経験/300
				上昇済++
			ENDIF
		CASE "レズ中毒"
			IF EXP:対象:レズ経験/300 > ABL:対象:レズ中毒
				ABL:対象:レズ中毒 = EXP:対象:レズ経験/300
				上昇済++
			ENDIF
		CASE "精液中毒"
			IF EXP:対象:精液経験/300 > ABL:対象:精液中毒
				ABL:対象:精液中毒 = EXP:対象:精液経験/300
				上昇済++
			ENDIF
		CASE "自慰中毒"
			IF EXP:対象:自慰経験/300 > ABL:対象:自慰中毒
				ABL:対象:自慰中毒 = EXP:対象:自慰経験/300
				上昇済++
			ENDIF
		CASE "性交中毒"
			IF EXP:対象:性交経験/150 > ABL:対象:性交中毒
				ABL:対象:性交中毒 = EXP:対象:性交経験/150
				上昇済++
			ENDIF
		CASE "マゾっ気"
			IF EXP:対象:苦痛快楽経験/150 > ABL:対象:マゾっ気
				ABL:対象:マゾっ気 = EXP:対象:苦痛快楽経験/150
				上昇済++
			ENDIF
		CASE "奉仕精神"
			DO
				SELECTCASE ABL:対象:LCOUNT
					CASE 0
						目標値 = 50
					CASE 1
						目標値 = 100
					CASE IS <10
						目標値 = ABL:対象:LCOUNT*300
					CASEELSE
						BREAK
				ENDSELECT
				SIF EXP:対象:奉仕快楽経験 < 目標値
					BREAK
				ABL:対象:LCOUNT++
				上昇済++
			LOOP 1
		CASE "感覚"
			$UPWITHJUEL感覚
			DO
				SELECTCASE ABL:対象:LCOUNT
					CASE 0
						目標値 = 1
					CASE 1
						目標値 = 20
					CASE 2
						目標値 = 400
					CASE 3
						目標値 = 5000
					CASE 4
						目標値 = 15000
					CASE 5
						目標値 = 30000
					CASE 6
						目標値 = 40000
					CASE 7
						目標値 = 50000
					CASE 8
						目標値 = 60000
					CASE 9
						目標値 = 70000
					CASEELSE
						BREAK
				ENDSELECT
				SIF JUEL:対象:対応珠 < 目標値
					BREAK
				JUEL:対象:対応珠 -= 目標値
				ABL:対象:LCOUNT++
				上昇済++
			LOOP 1
		CASE "珠使うやつ"
			$UPWITHJUEL
			DO
				SELECTCASE ABL:対象:LCOUNT
					CASE 0
						目標値 = 20
					CASE IS < 10
						目標値 = PALAMLV:(ABL:対象:LCOUNT)+1
					CASEELSE
						BREAK
				ENDSELECT
				SIF JUEL:対象:対応珠 < 目標値
					BREAK
				JUEL:対象:対応珠 -= 目標値
				ABL:対象:LCOUNT++
				上昇済++
			LOOP 1
		CASE "従順"
			計算用EXP = EXP:対象:奉仕快楽経験+EXP:対象:苦痛快楽経験
			DO
				SELECTCASE ABL:対象:従順
					CASE 0
						目標値 = 50
					CASE 1
						目標値 = 150
					CASE 2
						目標値 = 300
					CASE 3
						目標値 = 500
					CASE 4
						目標値 = 800
					CASE 5
						目標値 = 1200
					CASE 6
						目標値 = 1700
					CASE 7
						目標値 = 2300
					CASE 8
						目標値 = 3000
					CASE 9
						目標値 = 4000
					CASEELSE
						BREAK
				ENDSELECT
				SIF 計算用EXP < 目標値
					BREAK
				ABL:対象:従順++
				上昇済++
			LOOP 1
	ENDSELECT
	SIF ABL:対象:LCOUNT > 10
		ABL:対象:LCOUNT = 10
	SIF 上昇済
		PRINTFORML %NAME:対象%の%ABLNAME:LCOUNT%が{ABL:対象:LCOUNT}になった
NEXT

@SCHOOL_COMFILTER, 場所
#DIMS DYNAMIC 場所
#DIM DYNAMIC LCOUNT

USINGFILTERG = 0

VARSET COMFILTER, 1

TRYCALLFORM SCHOOL_COMFILTER_%CSVNAMERPS(NO:TARGET)%, 場所
SELECTCASE 場所
	CASE "自宅", "ホテル", "トイレ", "保健室", "体育倉庫", "校長室", "理事長室", "青姦"
	CASEELSE
		;#;THROW 「%場所%」のコマンドフィルターが設定されていない
ENDSELECT

FOR LCOUNT, 0, VARSIZE("TRAINNAME")
	SELECTCASE TRAINNAME:LCOUNT
		;ロケーション関係なく実行可能
		{
		CASE "愛撫", "クンニ", "指挿入れ", "フェラする", "下着扱きする", "手淫する",
			 "パイズリする", "パイズリフェラする", "射精寸止め","お掃除フェラする", "アナル舐め", "アナル愛撫",
			 "胸揉み", "乳首責め", "胸愛撫", "乳首舐め", "自慰", "胸自慰", "秘貝開帳", "自慰見せつけ", "秘貝開帳見せつけ",
			 "キスする", "キスしてもらう", "情欲を煽る", "スキンシップ", "母乳飲み", "尻尾を撫でる",
			 "ふたなり化", "バストサイズ変更", "コンドーム", "媚薬", "ローション注入", "後背位", "後背位アナル",
			 "後背位させる", "後背位アナルさせる", "手淫", "フェラチオ", "イラマチオ", "下着扱き", "素股", "パイズリ", "パイズリフェラ",
			 "縦パイズリ", "クンニさせる", "アナル愛撫させる", "アナルを舐めさせる", "兜合わせ", 
			 "Gスポット刺激", "授乳手コキ", "前立腺刺激", "オナフェラ", "騎乗乳房自慰", "挿入Gスポ責め", "挿入子宮口責め", "正常位キス", "騎乗位キス",
			 "正常位アナルキス", "騎乗位アナルキス", "後背位手淫", "騎乗位手淫", "後背位アナル手淫", "騎乗位アナル手淫", "正常位キスさせる",
			 "逆レイプキス"
		}
			COMFILTER:LCOUNT = 0
		;ベッドかソファが必要
		{
		CASE "正常位", "騎乗位", "下着ずらし挿入", "正常位アナル", "騎乗位アナル", "下着ずらし挿入アナル",
			 "正常位させる", "逆レイプ", "正常位アナルさせる", "顔面騎乗する", "貝合わせ", "シックスナイン", "69パイズリ",
		}
		SIF GROUPMATCH(場所, "自宅", "ホテル", "保健室", "体育倉庫", "校長室", "理事長室")
			COMFILTER:LCOUNT = 0
		;自宅とホテルは道具、風呂場使用可
		{
		CASE "ローター", "電気按摩", "バイブ", "ペニスバンド", "アナルバイブ", "乳首ローター",
			 "お風呂場プレイ", "シャワー", "腸洗浄"
		}
			SIF GROUPMATCH(場所, "自宅", "ホテル")
				COMFILTER:LCOUNT = 0
		;ローションを派手に使っても大丈夫な場所
		CASE "ローション"
			SIF GROUPMATCH(場所, "自宅", "ホテル", "青姦")
				COMFILTER:LCOUNT = 0
		;自宅限定
		CASE "お酒を飲ませる", "野外プレイ"
			SIF GROUPMATCH(場所, "自宅")
				COMFILTER:LCOUNT = 0
		;トイレと青姦のみ
		CASE "放尿"
			SIF GROUPMATCH(場所, "トイレ", "青姦")
				COMFILTER:LCOUNT = 0
	ENDSELECT
NEXT

@学園モード用コマンドテンプレ生成
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LCOUNT2
#DIM DYNAMIC フィルター記録, 1000
#DIMS DYNAMIC 場所リスト = "自宅", "ホテル", "トイレ", "保健室"

FOR LCOUNT, 0, VARSIZE("場所リスト")
	CALL SCHOOL_COMFILTER, 場所リスト:LCOUNT
	FOR LCOUNT2, 0, 1000
		SIF !COMFILTER:LCOUNT2
			SETBIT フィルター記録:LCOUNT2, LCOUNT
	NEXT
NEXT

CLEARLINE LINECOUNT

FOR LCOUNT, 0, 1000
	SIF !フィルター記録:LCOUNT
		CONTINUE
	PRINTL ;-------------------------------------------------
	PRINTFORML ;%TRAINNAME:LCOUNT%
	PRINT ;
	FOR LCOUNT2, 0, VARSIZE("場所リスト")
		SIF GETBIT(フィルター記録:LCOUNT, LCOUNT2)
			PRINTFORM %場所リスト:LCOUNT2%,
	NEXT
	PRINTL で実行可能
	PRINTL ;-------------------------------------------------
	PRINTFORML @SCHOOL_COM_%TRAINNAME:LCOUNT%_KX1
	PRINTL 
NEXT

OUTPUTLOG "SCHOOL_COM_KX1.ERB"
