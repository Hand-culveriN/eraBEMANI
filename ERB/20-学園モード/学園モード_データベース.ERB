
@学園データベース
#DIM DYNAMIC LCOUNT

CALL 学校一覧_データベース
RETURN 0


@学校一覧_データベース
#DIMS DYNAMIC 表示好感度
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LTARGET
#DIMS DYNAMIC 名称抜き取り用, 2
#DIMS DYNAMIC ロケーション名
#DIM DYNAMIC 選択校
#DIM DYNAMIC 選択キャラ
#DIM DYNAMIC 表示数
#DIM DYNAMIC 最大桁数
#DIM DYNAMIC 最大文字列長
#DIM DYNAMIC 改行カウント
#DIM DYNAMIC リスト表示
#DIM DYNAMIC 対象
#DIM DYNAMIC バストサイズ編集
#DIM DYNAMIC 風紀_人数カウント
#DIM DYNAMIC 風紀_カウント倍率
#DIM DYNAMIC 風紀_データベース用
#DIM DYNAMIC インモラル平均
#DIM DYNAMIC インモラル2乗平均
#DIM DYNAMIC インモラル標準偏差
#DIM DYNAMIC インモラル偏差値表示
#DIM CONST 無所属 = VARSIZE("学校名")

FOR LCOUNT, 0, VARSIZE("学校名")
	SIF STRLENS(学校名:LCOUNT) > 最大文字列長
		最大文字列長 = STRLENS(学校名:LCOUNT)
NEXT
;最大文字列長を偶数にする
SIF 最大文字列長%2
	最大文字列長++
PRINTFORML 侵入可%SPACES(7)%%SPACES((最大文字列長-6)/2)%学校名%SPACES((最大文字列長-6)/2)%
改行カウント = 3
FOR LCOUNT, 0, VARSIZE("学校名")
	IF 学校名:LCOUNT == ""
		IF 改行カウント
			改行カウント--
			PRINTL
		ENDIF
		CONTINUE
	ENDIF
	IF 学校名:LCOUNT == CSTR:MASTER:在校
		PRINTPLAIN 　★　
	ELSEIF 侵入可能:LCOUNT
		PRINTPLAIN 　○　
	ELSE
		PRINTPLAIN 　　　
	ENDIF
	PRINTFORM [{LCOUNT, 2}] - %学校名:LCOUNT%
	PRINTFORM %SPACES(最大文字列長 - STRLENS(学校名:LCOUNT))%
	IF ITEM:ラブスコープ
		PRINTPLAIN 　風紀:
		SELECTCASE 風紀:LCOUNT
			CASE 100 TO 199
				SETCOLORBYNAME lime
			CASE 200 TO 299
				SETCOLORBYNAME yellow
			CASE IS >= 300
				SETCOLORBYNAME coral
		ENDSELECT
		PRINTFORM {風紀:LCOUNT/10, 2}.{ABS(風紀:LCOUNT)%10}
		RESETCOLOR
	ENDIF
	PRINTL
NEXT
PRINTFORM 　　　[{無所属, 2}] - 無所属or社会人
PRINTFORM %SPACES(最大文字列長 - 14)%
PRINTL

DRAWLINE
PRINTPLAIN 　DATABASE　
PRINT [-1] - 戻る
IF ITEM:ラブスコープ
	SELECTCASE ラブスコープ使用
		CASE 0
			表示好感度 = 表示しない
		CASE 1 TO 6
			表示好感度 = %ERDNAME(好感度, ラブスコープ使用-1)%
		CASE 7
			表示好感度 = 好感度合計
		CASE 8
			表示好感度 = インモラル
	ENDSELECT
	PRINTFORM 　[-2] - ラブスコープの表示切り替え(現在:\@ 表示好感度 == "舎弟" ? 舎弟or母性 # %表示好感度% \@)
ENDIF
PRINT 　[-9] - 名前で検索
;#; PRINT 　[-99] - セーブデータのアップデート
PRINTL 

BINPUT
選択校 = RESULT
SELECTCASE RESULT
	CASE -1
		RETURN
	CASE -2
		CALL ラブスコープ設定変更
		RESTART
	CASE -9
		CALL LOOP_CLEARLINE, 1
		CALL CHARA_LIST, "全キャラ表示", 1, 1
		SIF RESULT == 10000
			RESTART
		選択キャラ = GETCHARA(RESULT)
		GOTO キャラ選択済み
	CASE -99
		;あまり古いセーブデータだと色々意図しない挙動が発生する事があるため一旦デバッグモードのみの機能
		PRINTFORML 現在のセーブデータが作成された以降に実装されたキャラを追加し、無所属キャラを新規入学・赴任させます
		PRINTFORML [0] アップデート処理を開始
		PRINTFORML [1] やめておく
		BINPUT
		IF !RESULT
			PRINTFORML 処理を開始します。しばらくお待ちください……
			CALL 学園モード開始, 1
			PRINTFORMW アップデート処理を完了しました
		ENDIF
		RESTART
ENDSELECT

SIF 選択校 < 0
	RESTART

VARSET 在校ID
VARSET 在任ID
VARSET 特殊在校ID
VARSET 在校人数
VARSET 在任人数
VARSET 特殊在校人数

風紀_人数カウント = 0
インモラル平均 = 0
インモラル2乗平均 = 0
;インモラルの重み付け平均※「風紀」と、平均との離れ具合(偏差値)を求める
;ただし、分散は定義通り求めるより「2乗の平均-平均の2乗」で求めるほうが簡単なのでそっちで求める
;※重み付けは平均・分散どちらにおいても、教師は2倍(保健は3倍/理事長・校長は5倍)の人数いるものとして計算する

FOR LCOUNT, 0, CHARANUM
	IF 選択校 == 無所属
		IF CSTR:LCOUNT:在校 == "" && CSTR:LCOUNT:在任 == ""
			在校ID:在校人数 = LCOUNT
			在校人数++
		ENDIF
	ELSE
		IF CSTR:LCOUNT:在校 == 学校名:選択校
			IF CFLAG:LCOUNT:特殊在校
				特殊在校ID:特殊在校人数 = LCOUNT
				特殊在校人数++
			ELSE
				在校ID:在校人数 = LCOUNT
				在校人数++
			ENDIF
			風紀_人数カウント++
			インモラル平均 += CFLAG:LCOUNT:インモラル
			インモラル2乗平均 += POWER(CFLAG:LCOUNT:インモラル, 2)
		ELSEIF CSTR:LCOUNT:在任 == 学校名:選択校
			在任ID:在任人数 = LCOUNT
			在任人数++
			SELECTCASE CSTR:LCOUNT:担当科目
				CASE "理事長", "校長"
					風紀_カウント倍率 = 5
				CASE "保健"
					風紀_カウント倍率 = 3
				CASEELSE
					風紀_カウント倍率 = 2
			ENDSELECT
			風紀_人数カウント += 風紀_カウント倍率
			インモラル平均 += 風紀_カウント倍率 * CFLAG:LCOUNT:インモラル
			インモラル2乗平均 += 風紀_カウント倍率 * POWER(CFLAG:LCOUNT:インモラル, 2)
		ENDIF
	ENDIF
NEXT

インモラル偏差値表示 = 0
IF 風紀_人数カウント
	;100倍で計算する
	インモラル平均 *= 100
	インモラル平均 /= 風紀_人数カウント
	インモラル2乗平均 *= 100
	インモラル2乗平均 /= 風紀_人数カウント

	;「風紀」は10倍で記録しておく
	;ただしデータベースで再計算した学校だけ風紀の値が変わるのは良くないのでこっちに保存
	風紀_データベース用 = インモラル平均/10

	;この時点では分散の100倍
	;分散は「2乗の平均-平均の2乗」で求める
	;平均はすでに100倍してあり2乗すると10000倍になるので100で割る
	インモラル標準偏差 = インモラル2乗平均 - POWER(インモラル平均, 2)/100

	;標準偏差は√(分散)で求まる
	;ここで標準偏差の10倍にする
	インモラル標準偏差 = SQRT(インモラル標準偏差)

	[IF_DEBUG]
		インモラル偏差値表示 = 1
	[ENDIF]
ENDIF

最大桁数 = 1
最大文字列長 = 4
IF 在校人数
	SIF DGT(在校ID:(在校人数-1)) > 最大桁数
		最大桁数 = DGT(在校ID:(在校人数-1))
ENDIF
IF 在任人数
	SIF DGT(在任ID:(在任人数-1)) > 最大桁数
		最大桁数 = DGT(在任ID:(在任人数-1))
ENDIF
IF 特殊在校人数
	SIF DGT(特殊在校ID:(特殊在校人数-1)) > 最大桁数
		最大桁数 = DGT(特殊在校ID:(特殊在校人数-1))
ENDIF
FOR LCOUNT, 0, 在校人数
	SIF STRLENS(NAME:(在校ID:LCOUNT)) > 最大文字列長
		最大文字列長 = STRLENS(NAME:(在校ID:LCOUNT))
NEXT
FOR LCOUNT, 0, 在任人数
	SIF STRLENS(NAME:(在任ID:LCOUNT)) > 最大文字列長
		最大文字列長 = STRLENS(NAME:(在任ID:LCOUNT))
NEXT
FOR LCOUNT, 0, 特殊在校人数
	SIF STRLENS(NAME:(特殊在校ID:LCOUNT)) > 最大文字列長
		最大文字列長 = STRLENS(NAME:(特殊在校ID:LCOUNT))
NEXT
;最大文字列長を偶数にする
SIF 最大文字列長%2
	最大文字列長++

$生徒選択
SIF !INRANGE(選択校, 0, VARSIZE("学校名"))
	RESTART

DRAWLINE
IF リスト表示
	IF 選択校 == 無所属
		PRINTL ■無所属or社会人
	ELSE
		PRINTFORML ■%学校名:選択校%
		IF インモラル偏差値表示
			PRINT 　インモラル平均(風紀):
			IF 風紀:選択校 != 風紀_データベース用
				PRINTFORM {風紀:選択校/10}.{ABS(風紀:選択校)%10}→
			ENDIF
			PRINTFORML {風紀_データベース用/10}.{ABS(風紀_データベース用)%10}　インモラル標準偏差:{インモラル標準偏差/10}.{インモラル標準偏差%10}
		ELSE
			PRINT 　風紀崩壊度:
			IF 風紀:選択校 != 風紀_データベース用
				PRINTFORM {風紀:選択校/10}.{ABS(風紀:選択校)%10}→
			ENDIF
			PRINTFORML {風紀_データベース用/10}.{ABS(風紀_データベース用)%10}
		ENDIF
	ENDIF
	PRINTFORM 恋人|%SPACES(2+最大桁数)%%SPACES((最大文字列長-4)/2)%名前%SPACES((最大文字列長-4)/2)%| 性別 |
	SIF MOD:バスト関連ステータス表示
		PRINT 　バスト　   トップ(カップ数)   |
	PRINT 　友情　|ときめき|　尊敬　|母性舎弟| セフレ |既成事実|好感度合計|
	IF インモラル偏差値表示
		PRINT ｲﾝﾓﾗﾙ/偏差値|
	ELSE
		PRINT インモラル|
	ENDIF
	PRINTL

	FOR LCOUNT, 0, 在校人数
		対象 = 在校ID:LCOUNT
		IF TALENT:対象:恋慕
			PRINT  ● 
		ELSE
			PRINT 　　
		ENDIF 
		PRINTPLAIN |
		PRINTFORM [{対象, 最大桁数}]
		CALL リスト_名前表示, 対象, 最大文字列長
		PRINTPLAIN |
		CALL リスト_性別表示, 対象
		PRINTPLAIN |
		SIF MOD:バスト関連ステータス表示
			CALL リスト_バストサイズ, 対象
		IF インモラル偏差値表示
			CALL 好感度表示, 対象, 風紀_データベース用, インモラル標準偏差
		ELSE
			CALL 好感度表示, 対象
		ENDIF
		
		PRINTL
	NEXT
	IF 選択校 != 無所属
		FOR LCOUNT, 0, 在任人数
			対象 = 在任ID:LCOUNT
			IF TALENT:対象:恋慕
				PRINT  ● 
			ELSE
				PRINT 　　
			ENDIF 
			PRINTPLAIN |
			PRINTFORM [{対象, 最大桁数}]
			CALL リスト_名前表示, 対象, 最大文字列長
			PRINTPLAIN |
			CALL リスト_性別表示, 対象
			PRINTPLAIN |
			SIF MOD:バスト関連ステータス表示
				CALL リスト_バストサイズ, 対象
			IF インモラル偏差値表示
				CALL 好感度表示, 対象, 風紀_データベース用, インモラル標準偏差
			ELSE
				CALL 好感度表示, 対象
			ENDIF
			PRINTL
		NEXT
	ENDIF
	IF 選択校 != 無所属
		FOR LCOUNT, 0, 特殊在校人数
			対象 = 特殊在校ID:LCOUNT
			IF TALENT:対象:恋慕
				PRINT  ● 
			ELSE
				PRINT 　　
			ENDIF 
			PRINTPLAIN |
			PRINTFORM [{対象, 最大桁数}]
			CALL リスト_名前表示, 対象, 最大文字列長
			PRINTPLAIN |
			CALL リスト_性別表示, 対象
			PRINTPLAIN |
			SIF MOD:バスト関連ステータス表示
				CALL リスト_バストサイズ, 対象
			IF インモラル偏差値表示
				CALL 好感度表示, 対象, 風紀_データベース用, インモラル標準偏差
			ELSE
				CALL 好感度表示, 対象
			ENDIF
			PRINTL
		NEXT
	ENDIF
ELSE
	PRINTFORML □\@ 選択校 == 無所属 ? 無所属or社会人のキャラ # %学校名:選択校%の在校生 \@
	FOR LCOUNT, 0, 在校人数
		PRINTFORM 　[{在校ID:LCOUNT, 最大桁数}] 
		CALL SNAME(在校ID:LCOUNT)
		PRINTL 
	NEXT
	IF 選択校 != 無所属
		PRINTFORML □%学校名:選択校%の教師
		FOR LCOUNT, 0, 在任人数
			PRINTFORM 　担当科目:%CSTR:(在任ID:LCOUNT):担当科目% [{在任ID:LCOUNT, 最大桁数}] 
			CALL SNAME(在任ID:LCOUNT, 1)
			PRINTL  
		NEXT
	ENDIF
	IF 選択校 != 無所属
		PRINTFORML □その他%学校名:選択校%に居るキャラ
		FOR LCOUNT, 0, 特殊在校人数
			PRINTFORM 　[{特殊在校ID:LCOUNT, 最大桁数}] 
			CALL SNAME(特殊在校ID:LCOUNT)
			PRINTL  
		NEXT
	ENDIF
ENDIF

DRAWLINE
PRINTPLAINFORM 　\@ 選択校 == 無所属 ? 無所属or社会人のキャラ # %学校名:選択校% \@　
PRINT [-1] - 戻る
IF ITEM:ラブスコープ
	SELECTCASE ラブスコープ使用
		CASE 0
			表示好感度 = 表示しない
		CASE 1 TO 6
			表示好感度 = %ERDNAME(好感度, ラブスコープ使用-1)%
		CASE 7
			表示好感度 = 好感度合計
		CASE 8
			表示好感度 = インモラル
	ENDSELECT
	PRINTFORM 　[-2] - ラブスコープの表示切り替え(現在:\@ 表示好感度 == "舎弟" ? 舎弟or母性 # %表示好感度% \@)
	PRINT 　[-3] - リスト表示
	SIF MOD:バスト関連ステータス表示
		PRINTFORM 　[-8] - バストサイズ編集(現在:\@ バストサイズ編集 ? あり # なし \@)
ENDIF
PRINTL

BINPUT
IF RESULT == -1
	RESTART
ELSEIF RESULT == -2
	CALL ラブスコープ設定変更
	GOTO 生徒選択
ELSEIF RESULT == -3
	INVERTBIT リスト表示, 0
	GOTO 生徒選択
ELSEIF RESULT == -8
	INVERTBIT バストサイズ編集, 0
	GOTO 生徒選択
ENDIF

選択キャラ = RESULT
$キャラ選択済み
IF バストサイズ編集
	CALL バストサイズ詳細入力, NOF(選択キャラ)
	GOTO 生徒選択
ENDIF
DRAWLINE
PRINTFORML □%NAME:選択キャラ%の情報
PRINT 　所属:
IF CSTR:選択キャラ:在校 != ""
	PRINTFORM %CSTR:選択キャラ:在校%\@ CFLAG:選択キャラ:特殊在校 ? (その他) # (生徒) \@
ELSEIF CSTR:選択キャラ:在任 != ""
	PRINTFORM %CSTR:選択キャラ:在任%(%CSTR:選択キャラ:担当科目%)
ELSE
	PRINT 無所属
ENDIF
PRINTL 
PRINTFORML 　インモラル:{CFLAG:選択キャラ:インモラル}
IF 選択キャラ != PLAYER
	IF ラブスコープ使用
		PRINTL □好感度
		PRINTFORMLC 　友情:{好感度:選択キャラ:友情}
		PRINTFORMLC ときめき:{好感度:選択キャラ:ときめき}
		PRINTFORMLC 尊敬:{好感度:選択キャラ:尊敬}
		PRINTL 
		PRINTFORMLC 　\@ SEX(選択キャラ) == 2 || NAME:選択キャラ == "ハニー" ? 母性 # 舎弟 \@:{好感度:選択キャラ:舎弟}
		PRINTFORMLC セフレ:{好感度:選択キャラ:セフレ}
		PRINTFORMLC 既成事実:{好感度:選択キャラ:既成事実}
		PRINTL 
	ENDIF
	PRINTL □各ロケーションでの出現率
	;どこのロケーションに出現するのかを表示
	データベース参照中 = 選択キャラ
	表示数 = 0
	FOR LCOUNT, 0, VARSIZE("ロケーション")
		VARSET 出現率
		SPLIT ロケーション:LCOUNT, "/", 名称抜き取り用
		;分割されてたらスラッシュより後の部分だけを使用する
		IF RESULT >= 2
			ロケーション名 = %RPS(名称抜き取り用:1)%
		ELSE
			ロケーション名 = %RPS(ロケーション:LCOUNT)%
		ENDIF
		TRYCALLFORM 出現キャラ_%RPS(ロケーション名)%
		;口上による出現率変更
		IF OPTION:口上表示 && !CFLAG:LCOUNT:口上非表示 && EXISTFUNCTION(@"SCHOOL_%CSVNAMERPS(NO:選択キャラ)%_出現率設定")
			LTARGET = TARGET
			TARGET = 選択キャラ
			CALLFORM SCHOOL_%CSVNAMERPS(NO:選択キャラ)%_出現率設定, ロケーション名
			IF RESULT < 0
				出現率:選択キャラ = 0
			ELSEIF RESULT > 0
				出現率:選択キャラ = LIMIT(RESULT, 0, 100)
			ENDIF
			TARGET = LTARGET
		ENDIF
		;出現率も表示
		SIF !出現率:選択キャラ
			CONTINUE
		PRINTFORMLC 　%ロケーション:LCOUNT%({出現率:選択キャラ}\%)
		表示数++
		SIF 表示数 != 0 && 表示数%4 == 0
			PRINTL 
	NEXT
	データベース参照中 = 0
	CALL NEWLINE
ENDIF
DRAWLINE
PRINTPLAIN 　DATABASE　
PRINTW クリック・Enterで戻る

GOTO 生徒選択




@リスト_名前表示, 対象, 最大文字列長
#DIM DYNAMIC 対象
#DIM DYNAMIC 最大文字列長

SIF NAME:対象 == 運命の人
	SETCOLORBYNAME coral
PRINTFORM %NAME:対象%%SPACES(最大文字列長-STRLENS(NAME:対象))%
RESETCOLOR

@リスト_性別表示, 対象
#DIM DYNAMIC 対象
IF TALENT:対象:性転換済
	IF SEX(対象) == 1
		PRINT ♀→♂
	ELSE
		PRINT ♂→♀
	ENDIF
ELSE
	IF SEX(対象) == 1
		PRINT 　♂　
	ELSE
		PRINT 　♀　
	ENDIF
ENDIF

@リスト_バストサイズ, 対象
#DIM DYNAMIC 対象
#DIM DYNAMIC 対象キャラ番号
#DIM DYNAMIC 今のサイズ
#DIM DYNAMIC トップ
#DIM DYNAMIC アンダー
#DIM DYNAMIC 成長割合
#DIM DYNAMIC ワンサイズ増分

対象キャラ番号 = NOF(対象)
成長割合 = LIMIT(CFLAG:対象:バスト成長, 0, 999)
今のサイズ = TALENT:対象:バストサイズ

SIF !今のサイズ
	SETCOLOR 0x404040

IF バストサイズ詳細トップ:対象キャラ番号:今のサイズ
	トップ = バストサイズ詳細トップ:対象キャラ番号:今のサイズ
	アンダー = バストサイズ詳細アンダー:対象キャラ番号:今のサイズ
	IF 成長割合
		IF バストサイズ詳細トップ:対象キャラ番号:(今のサイズ+1)
			ワンサイズ増分 = バストサイズ詳細トップ:対象キャラ番号:(今のサイズ+1) - トップ
			トップ += ワンサイズ増分 * 成長割合 / 1000
			IF アンダー && バストサイズ詳細アンダー:対象キャラ番号:(今のサイズ+1)
				ワンサイズ増分 = バストサイズ詳細アンダー:対象キャラ番号:(今のサイズ+1) - アンダー
				アンダー += ワンサイズ増分 * 成長割合 / 1000
			ENDIF
		ENDIF
	ENDIF 
	PRINTFORM %CBL_TALENT(今のサイズ, 成長割合)% %CBL_SIZE(トップ,アンダー)%
ELSE
	PRINTFORM %CBL_TALENT(今のサイズ, 成長割合)% %SPACES(7+13)%
ENDIF
RESETCOLOR
PRINT |

@好感度表示, 対象, インモラル平均=0, インモラル標準偏差=-1
#DIM DYNAMIC 対象
#DIM DYNAMIC 好感度値
#DIM DYNAMIC インモラル平均
#DIM DYNAMIC インモラル標準偏差
#DIM DYNAMIC 偏差値
#DIM DYNAMIC LCOUNT

FOR LCOUNT, 0, 5
	好感度値 = 好感度:対象:LCOUNT
	SELECTCASE 好感度値
		CASE 0
			SETCOLORBYNAME black
		CASE 1 TO 99
		CASE 100 TO 299
			SETCOLORBYNAME lime
		CASE 300 TO 499
			SETCOLORBYNAME deepskyblue
		CASE 500 TO 999
			SETCOLORBYNAME violet
		CASE IS >= 1000
			SETCOLORBYNAME coral
	ENDSELECT
	PRINTFORM {好感度値, 8}
	RESETCOLOR
	PRINT |
NEXT
好感度値 = 好感度:対象:5
SELECTCASE 好感度値
	CASE 0
		SETCOLORBYNAME black
	CASE 1 TO 4
	CASE 5 TO 9
		SETCOLORBYNAME lime
	CASE 10 TO 14
		SETCOLORBYNAME deepskyblue
	CASE 15 TO 19
		SETCOLORBYNAME violet
	CASE IS >= 20
		SETCOLORBYNAME coral
ENDSELECT
PRINTFORM {好感度値, 8}
RESETCOLOR
PRINT |
好感度値 = 好感度合計(対象)
SELECTCASE 好感度値
	CASE 0
		SETCOLORBYNAME black
	CASE 1 TO 99
	CASE 100 TO 299
		SETCOLORBYNAME lime
	CASE 300 TO 499
		SETCOLORBYNAME deepskyblue
	CASE 500 TO 999
		SETCOLORBYNAME violet
	CASE IS >= 1000
		SETCOLORBYNAME coral
ENDSELECT
PRINTFORM {好感度値, 10}
RESETCOLOR
PRINT |
好感度値 = CFLAG:対象:インモラル
SELECTCASE 好感度値
	CASE 0
		SETCOLORBYNAME black
	CASE 10 TO 29
		SETCOLORBYNAME lime
	CASE 30 TO 49
		SETCOLORBYNAME yellow
	CASE IS >= 50
		SETCOLORBYNAME coral
ENDSELECT
IF インモラル標準偏差 >= 0
	偏差値 = 500
	SIF インモラル標準偏差
		偏差値 += 100*(10*好感度値-インモラル平均)/インモラル標準偏差
	SIF !好感度値
		SETCOLOR 0x404040
	PRINTFORM {好感度値, 5}
	RESETCOLOR
	PRINT /
	PRINTFORM {偏差値/10, 4}.{偏差値%10}
ELSE
	PRINTFORM {好感度値, 10}
	RESETCOLOR
ENDIF
PRINT |
