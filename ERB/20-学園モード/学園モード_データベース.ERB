
@学園データベース
#DIM DYNAMIC LCOUNT

CALL 学校一覧_データベース
RETURN 0


@学校一覧_データベース
#DIMS DYNAMIC 表示好感度
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC LTARGET
#DIMS DYNAMIC 名称抜き取り用, 2
#DIMS DYNAMIC ロケーション名
#DIM DYNAMIC 選択校
#DIM DYNAMIC 選択キャラ
#DIM DYNAMIC 表示数
#DIM DYNAMIC 改行カウント
#DIM DYNAMIC リスト表示
#DIM DYNAMIC バストサイズ編集
#DIM DYNAMIC 最大文字列長
#DIM CONST 無所属 = VARSIZE("学校名")

FOR LCOUNT, 0, VARSIZE("学校名")
	SIF STRLENS(学校名:LCOUNT) > 最大文字列長
		最大文字列長 = STRLENS(学校名:LCOUNT)
NEXT
;最大文字列長を偶数にする
SIF 最大文字列長%2
	最大文字列長++
PRINTFORML 侵入可%SPACES(7)%%SPACES((最大文字列長-6)/2)%学校名%SPACES((最大文字列長-6)/2)%
改行カウント = 3
FOR LCOUNT, 0, VARSIZE("学校名")
	IF 学校名:LCOUNT == ""
		IF 改行カウント
			改行カウント--
			PRINTL
		ENDIF
		CONTINUE
	ENDIF
	IF 学校名:LCOUNT == CSTR:MASTER:在校
		PRINTPLAIN 　★　
	ELSEIF 侵入可能:LCOUNT
		PRINTPLAIN 　○　
	ELSE
		PRINTPLAIN 　　　
	ENDIF
	PRINTFORM [{LCOUNT, 2}] - %学校名:LCOUNT%
	PRINTFORM %SPACES(最大文字列長 - STRLENS(学校名:LCOUNT))%
	IF ITEM:ラブスコープ
		PRINTPLAIN 　風紀:
		SELECTCASE 風紀:LCOUNT
			CASE 100 TO 199
				SETCOLORBYNAME lime
			CASE 200 TO 299
				SETCOLORBYNAME yellow
			CASE IS >= 300
				SETCOLORBYNAME coral
		ENDSELECT
		PRINTFORM {風紀:LCOUNT/10, 2}.{ABS(風紀:LCOUNT)%10}
		RESETCOLOR
	ENDIF
	PRINTL
NEXT
PRINTFORM 　　　[{無所属, 2}] - 無所属or社会人
PRINTFORM %SPACES(最大文字列長 - 14)%
PRINTL

DRAWLINE
PRINTPLAIN 　DATABASE　
PRINT [-1] - 戻る
IF ITEM:ラブスコープ
	SELECTCASE ラブスコープ使用
		CASE 0
			表示好感度 = 表示しない
		CASE 1 TO 6
			表示好感度 = %ERDNAME(好感度, ラブスコープ使用-1)%
		CASE 7
			表示好感度 = 好感度合計
		CASE 8
			表示好感度 = インモラル
	ENDSELECT
	PRINTFORM 　[-2] - ラブスコープの表示切り替え(現在:\@ 表示好感度 == "舎弟" ? 舎弟or母性 # %表示好感度% \@)
ENDIF
PRINT 　[-9] - 名前で検索
;#; PRINT 　[-99] - セーブデータのアップデート
PRINTL 

BINPUT
選択校 = RESULT
SELECTCASE RESULT
	CASE -1
		RETURN
	CASE -2
		CALL ラブスコープ設定変更
		RESTART
	CASE -9
		CALL LOOP_CLEARLINE, 1
		CALL CHARA_LIST, "全キャラ表示", 1, 1
		SIF RESULT == 10000
			RESTART
		選択キャラ = GETCHARA(RESULT)
		GOTO キャラ選択済み
	CASE -99
		;あまり古いセーブデータだと色々意図しない挙動が発生する事があるため一旦デバッグモードのみの機能
		PRINTFORML 現在のセーブデータが作成された以降に実装されたキャラを追加し、無所属キャラを新規入学・赴任させます
		PRINTFORML [0] アップデート処理を開始
		PRINTFORML [1] やめておく
		BINPUT
		IF !RESULT
			PRINTFORML 処理を開始します。しばらくお待ちください……
			CALL 学園モード開始, 1
			PRINTFORMW アップデート処理を完了しました
		ENDIF
		RESTART
ENDSELECT

$生徒選択
SIF !INRANGE(選択校, 0, VARSIZE("学校名"))
	RESTART
CALL 学園_データベース表示, 選択校, リスト表示

DRAWLINE
PRINTPLAINFORM 　\@ 選択校 == 無所属 ? 無所属or社会人のキャラ # %学校名:選択校% \@　
PRINT [-1] - 戻る
IF ITEM:ラブスコープ
	SELECTCASE ラブスコープ使用
		CASE 0
			表示好感度 = 表示しない
		CASE 1 TO 6
			表示好感度 = %ERDNAME(好感度, ラブスコープ使用-1)%
		CASE 7
			表示好感度 = 好感度合計
		CASE 8
			表示好感度 = インモラル
	ENDSELECT
	PRINTFORM 　[-2] - ラブスコープの表示切り替え(現在:\@ 表示好感度 == "舎弟" ? 舎弟or母性 # %表示好感度% \@)
	PRINT 　[-3] - リスト表示
	SIF MOD:バスト関連ステータス表示
		PRINTFORM 　[-8] - バストサイズ編集(現在:\@ バストサイズ編集 ? あり # なし \@)
ENDIF
PRINTL

BINPUT
SELECTCASE RESULT
	CASE -1
		RESTART
	CASE -2
		CALL ラブスコープ設定変更
		GOTO 生徒選択
	CASE -3
		INVERTBIT リスト表示, 0
		GOTO 生徒選択
	CASE -8
		INVERTBIT バストサイズ編集, 0
		GOTO 生徒選択
ENDSELECT

選択キャラ = RESULT
$キャラ選択済み
IF バストサイズ編集
	CALL バストサイズ詳細入力, NOF(選択キャラ)
	GOTO 生徒選択
ENDIF
DRAWLINE
PRINTFORML □%NAME:選択キャラ%の情報
PRINT 　所属:
IF CSTR:選択キャラ:在校 != ""
	PRINTFORM %CSTR:選択キャラ:在校%\@ CFLAG:選択キャラ:特殊在校 ? (その他) # (生徒) \@
ELSEIF CSTR:選択キャラ:在任 != ""
	PRINTFORM %CSTR:選択キャラ:在任%(%CSTR:選択キャラ:担当科目%)
ELSE
	PRINT 無所属
ENDIF
PRINTL 
PRINTFORML 　インモラル:{CFLAG:選択キャラ:インモラル}
IF 選択キャラ != PLAYER
	IF ラブスコープ使用
		PRINTL □好感度
		PRINTFORMLC 　友情:{好感度:選択キャラ:友情}
		PRINTFORMLC ときめき:{好感度:選択キャラ:ときめき}
		PRINTFORMLC 尊敬:{好感度:選択キャラ:尊敬}
		PRINTL 
		PRINTFORMLC 　\@ SEX(選択キャラ) == 2 || NAME:選択キャラ == "ハニー" ? 母性 # 舎弟 \@:{好感度:選択キャラ:舎弟}
		PRINTFORMLC セフレ:{好感度:選択キャラ:セフレ}
		PRINTFORMLC 既成事実:{好感度:選択キャラ:既成事実}
		PRINTL 
	ENDIF
	PRINTL □各ロケーションでの出現率
	;どこのロケーションに出現するのかを表示
	データベース参照中 = 選択キャラ
	表示数 = 0
	FOR LCOUNT, 0, VARSIZE("ロケーション")
		VARSET 出現率
		SPLIT ロケーション:LCOUNT, "/", 名称抜き取り用
		;分割されてたらスラッシュより後の部分だけを使用する
		IF RESULT >= 2
			ロケーション名 = %RPS(名称抜き取り用:1)%
		ELSE
			ロケーション名 = %RPS(ロケーション:LCOUNT)%
		ENDIF
		TRYCALLFORM 出現キャラ_%RPS(ロケーション名)%
		;口上による出現率変更
		IF OPTION:口上表示 && !CFLAG:LCOUNT:口上非表示 && EXISTFUNCTION(@"SCHOOL_%CSVNAMERPS(NO:選択キャラ)%_出現率設定")
			LTARGET = TARGET
			TARGET = 選択キャラ
			CALLFORM SCHOOL_%CSVNAMERPS(NO:選択キャラ)%_出現率設定, ロケーション名
			IF RESULT < 0
				出現率:選択キャラ = 0
			ELSEIF RESULT > 0
				出現率:選択キャラ = LIMIT(RESULT, 0, 100)
			ENDIF
			TARGET = LTARGET
		ENDIF
		;出現率も表示
		SIF !出現率:選択キャラ
			CONTINUE
		PRINTFORMLC 　%ロケーション:LCOUNT%({出現率:選択キャラ}\%)
		表示数++
		SIF 表示数 != 0 && 表示数%4 == 0
			PRINTL 
	NEXT
	データベース参照中 = 0
	CALL NEWLINE
ENDIF
DRAWLINE
PRINTPLAIN 　DATABASE　
PRINTW クリック・Enterで戻る

GOTO 生徒選択


@学園_データベース表示, 選択校, リスト表示
#DIM DYNAMIC LCOUNT
#DIM DYNAMIC 選択校
#DIM DYNAMIC 計算済
#DIM DYNAMIC リスト表示
#DIM CONST 無所属 = VARSIZE("学校名")
#DIMS DYNAMIC 表示用

#DIM DYNAMIC 表示キャラリスト, 10000
#DIM DYNAMIC COUNTMODE
#DIM DYNAMIC 人数
#DIM DYNAMIC 生徒人数
#DIM DYNAMIC 教師人数
#DIM DYNAMIC 特殊在校人数
#DIM DYNAMIC 対象

#DIM DYNAMIC 最大桁数 = 1
#DIM DYNAMIC 最大文字列長 = 4
#DIM DYNAMIC 名前欄幅

#DIM DYNAMIC 風紀_人数カウント
#DIM DYNAMIC 風紀_カウント倍率
#DIM DYNAMIC 風紀_データベース用
#DIM DYNAMIC インモラル平均
#DIM DYNAMIC インモラル2乗平均
#DIM DYNAMIC インモラル標準偏差
#DIM DYNAMIC インモラル偏差値表示

;インモラルの重み付け平均※「風紀」と、平均との離れ具合(偏差値)を求める
;ただし、分散は定義通り求めるより「2乗の平均-平均の2乗」で求めるほうが簡単なのでそっちで求める
;※重み付けは平均・分散どちらにおいても、教師は2倍(保健は3倍/理事長・校長は5倍)の人数いるものとして計算する

FOR COUNTMODE, 0, 4
	IF 選択校 == 無所属
		SIF COUNTMODE < 3
			COUNTMODE = 3
	ELSE
		SIF COUNTMODE >= 3
			BREAK
	ENDIF
	FOR LCOUNT, 0, CHARANUM
		SELECTCASE COUNTMODE
			CASE 0
				SIF CSTR:LCOUNT:在校 != 学校名:選択校 || CFLAG:LCOUNT:特殊在校
					CONTINUE
				生徒人数++
			CASE 1
				SIF CSTR:LCOUNT:在任 != 学校名:選択校
					CONTINUE
				教師人数++
			CASE 2
				SIF CSTR:LCOUNT:在校 != 学校名:選択校 || !CFLAG:LCOUNT:特殊在校
					CONTINUE
				特殊在校人数++
			CASE 3
				SIF CSTR:LCOUNT:在校 != "" || CSTR:LCOUNT:在任 != ""
					CONTINUE
		ENDSELECT
		表示キャラリスト:(人数++) = LCOUNT
		SIF DGT(LCOUNT) > 最大桁数
			最大桁数 = DGT(LCOUNT)
		SIF STRLENS(NAME:LCOUNT) > 最大文字列長
			最大文字列長 = STRLENS(NAME:LCOUNT)
		
		IF 選択校 != 無所属
			SELECTCASE CSTR:LCOUNT:担当科目
				CASE "理事長", "校長"
					風紀_カウント倍率 = 5
				CASE "保健"
					風紀_カウント倍率 = 3
				CASE ""
					;生徒は等倍
					風紀_カウント倍率 = 1
				CASEELSE
					風紀_カウント倍率 = 2
			ENDSELECT
			風紀_人数カウント += 風紀_カウント倍率
			インモラル平均 += 風紀_カウント倍率 * CFLAG:LCOUNT:インモラル
			インモラル2乗平均 += 風紀_カウント倍率 * POWER(CFLAG:LCOUNT:インモラル, 2)
		ENDIF
	NEXT
NEXT
;最大文字列長を偶数にする
SIF 最大文字列長%2
	最大文字列長++
名前欄幅 = 2+最大桁数+最大文字列長

インモラル偏差値表示 = 0
IF 風紀_人数カウント
	;100倍で計算する
	インモラル平均 *= 100
	インモラル平均 /= 風紀_人数カウント
	インモラル2乗平均 *= 100
	インモラル2乗平均 /= 風紀_人数カウント

	;「風紀」は10倍で記録しておく
	;ただしデータベースで再計算した学校だけ風紀の値が変わるのは良くないのでこっちに保存
	風紀_データベース用 = インモラル平均/10

	;この時点では分散の100倍
	;分散は「2乗の平均-平均の2乗」で求める
	;平均はすでに100倍してあり2乗すると10000倍になるので100で割る
	インモラル標準偏差 = インモラル2乗平均 - POWER(インモラル平均, 2)/100

	;標準偏差は√(分散)で求まる
	;ここで標準偏差の10倍にする
	インモラル標準偏差 = SQRT(インモラル標準偏差)

	[IF_DEBUG]
		インモラル偏差値表示 = 1
	[ENDIF]
ENDIF


DRAWLINE
IF リスト表示
	IF 選択校 == 無所属
		PRINTL ■無所属or社会人
	ELSE
		PRINTFORML ■%学校名:選択校%
		IF インモラル偏差値表示
			PRINT 　インモラル平均(風紀):
			IF 風紀:選択校 != 風紀_データベース用
				PRINTFORM {風紀:選択校/10}.{ABS(風紀:選択校)%10}→
			ENDIF
			PRINTFORML {風紀_データベース用/10}.{ABS(風紀_データベース用)%10}　インモラル標準偏差:{インモラル標準偏差/10}.{インモラル標準偏差%10}
		ELSE
			PRINT 　風紀崩壊度:
			IF 風紀:選択校 != 風紀_データベース用
				PRINTFORM {風紀:選択校/10}.{ABS(風紀:選択校)%10}→
			ENDIF
			PRINTFORML {風紀_データベース用/10}.{ABS(風紀_データベース用)%10}
		ENDIF
	ENDIF

	FOR LCOUNT, 0, 人数
		表示用 = 
		IF !LCOUNT
			IF 選択校 == 無所属
				表示用 = 氏名
			ELSE
				表示用 = 生徒氏名
			ENDIF
		ELSEIF 選択校 != 無所属
			SIF LCOUNT == 生徒人数 && 教師人数
				表示用 = 教員氏名
			SIF LCOUNT == 生徒人数+教師人数 && 特殊在校人数
				表示用 = 特殊在校者氏名
		ENDIF
		IF 表示用 != ""
			表示用 = %SPACES((名前欄幅-STRLENS(表示用))/2)%%表示用%
			PRINTFORM 恋人|%表示用, 名前欄幅, LEFT%| 性別 |
			SIF MOD:バスト関連ステータス表示
				PRINT 　バスト　   トップ(カップ数)   |
			PRINT 　友情　|ときめき|　尊敬　|母性舎弟| セフレ |既成事実|好感度合計|
			IF インモラル偏差値表示
				PRINT ｲﾝﾓﾗﾙ/偏差値|
			ELSE
				PRINT インモラル|
			ENDIF
			PRINTL
		ENDIF
		
		対象 = 表示キャラリスト:LCOUNT
		IF TALENT:対象:恋慕
			PRINT  ● 
		ELSE
			PRINT 　　
		ENDIF 
		PRINTPLAIN |
		PRINTFORM [{対象, 最大桁数}]
		
		SIF NAME:対象 == 運命の人
			SETCOLORBYNAME coral
		PRINTFORM %NAME:対象%%SPACES(最大文字列長-STRLENS(NAME:対象))%
		RESETCOLOR
		
		PRINTPLAIN |
		
		IF TALENT:対象:性転換済
			IF SEX(対象) == 1
				PRINT ♀→♂
			ELSE
				PRINT ♂→♀
			ENDIF
		ELSE
			IF SEX(対象) == 1
				PRINT 　♂　
			ELSE
				PRINT 　♀　
			ENDIF
		ENDIF
		
		PRINTPLAIN |
		SIF MOD:バスト関連ステータス表示
			CALL リスト_バストサイズ, 対象
		IF インモラル偏差値表示
			CALL 好感度表示, 対象, 風紀_データベース用, インモラル標準偏差
		ELSE
			CALL 好感度表示, 対象
		ENDIF
		PRINTL
	NEXT
ELSE
	PRINTFORML □\@ 選択校 == 無所属 ? 無所属or社会人のキャラ # %学校名:選択校%の在校生 \@
	FOR LCOUNT, 0, 人数
		IF 選択校 != 無所属
			SIF LCOUNT == 生徒人数 && 教師人数
				PRINTFORML □%学校名:選択校%の教師
			SIF LCOUNT == 生徒人数+教師人数 && 特殊在校人数
				PRINTFORML □その他%学校名:選択校%に居るキャラ
		ENDIF
		PRINTFORM 　[{表示キャラリスト:LCOUNT, 最大桁数}] 
		CALL SNAME(表示キャラリスト:LCOUNT, 1)
		PRINTL 
	NEXT
ENDIF


@リスト_バストサイズ, 対象
#DIM DYNAMIC 対象
#DIM DYNAMIC 対象キャラ番号
#DIM DYNAMIC 今のサイズ
#DIM DYNAMIC トップ
#DIM DYNAMIC アンダー
#DIM DYNAMIC 成長割合
#DIM DYNAMIC ワンサイズ増分

対象キャラ番号 = NOF(対象)
成長割合 = LIMIT(CFLAG:対象:バスト成長, 0, 999)
今のサイズ = TALENT:対象:バストサイズ

SIF !今のサイズ
	SETCOLOR 0x404040

IF バストサイズ詳細トップ:対象キャラ番号:今のサイズ
	トップ = バストサイズ詳細トップ:対象キャラ番号:今のサイズ
	アンダー = バストサイズ詳細アンダー:対象キャラ番号:今のサイズ
	IF 成長割合
		IF バストサイズ詳細トップ:対象キャラ番号:(今のサイズ+1)
			ワンサイズ増分 = バストサイズ詳細トップ:対象キャラ番号:(今のサイズ+1) - トップ
			トップ += ワンサイズ増分 * 成長割合 / 1000
			IF アンダー && バストサイズ詳細アンダー:対象キャラ番号:(今のサイズ+1)
				ワンサイズ増分 = バストサイズ詳細アンダー:対象キャラ番号:(今のサイズ+1) - アンダー
				アンダー += ワンサイズ増分 * 成長割合 / 1000
			ENDIF
		ENDIF
	ENDIF 
	PRINTFORM %CBL_TALENT(今のサイズ, 成長割合)% %CBL_SIZE(トップ,アンダー)%
ELSE
	PRINTFORM %CBL_TALENT(今のサイズ, 成長割合)% %SPACES(7+13)%
ENDIF
RESETCOLOR
PRINT |

@好感度表示, 対象, インモラル平均 = 0, インモラル標準偏差 = -1
#DIM DYNAMIC 対象
#DIM DYNAMIC 好感度値
#DIM DYNAMIC インモラル平均
#DIM DYNAMIC インモラル標準偏差
#DIM DYNAMIC 偏差値
#DIM DYNAMIC LCOUNT

FOR LCOUNT, 0, 6
	好感度値 = 好感度:対象:LCOUNT
	IF !好感度値
		PRINTFORM %SPACES(8)%
	ELSE
		CALL PRINTCOLOR, @"{好感度値, 8}", 好感度色分け(ERDNAME(好感度, LCOUNT), 好感度値, 1)
	ENDIF
	PRINT |
NEXT

IF !好感度合計(対象)
	PRINTFORM %SPACES(10)%
ELSE
	CALL PRINTCOLOR, @"{好感度合計(対象), 10}", 好感度色分け("好感度合計", 好感度合計(対象), 1)
ENDIF
PRINT |

好感度値 = CFLAG:対象:インモラル
IF インモラル標準偏差 >= 0
	偏差値 = 500
	SIF インモラル標準偏差
		偏差値 += 100*(10*好感度値-インモラル平均)/インモラル標準偏差
	CALL PRINTCOLOR, @"{好感度値, 5}", 好感度色分け("インモラル", 好感度値)
	PRINT /
	PRINTFORM {偏差値/10, 4}.{偏差値%10}
ELSE
	CALL PRINTCOLOR, @"{好感度値, 10}", 好感度色分け("インモラル", 好感度値)
ENDIF
PRINT |
