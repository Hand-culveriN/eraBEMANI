@USERCOM_ABLE_ローションガーゼ
#FUNCTION
#DIM DYNAMIC 条件確定
;ゲーセンに居る場合はトイレじゃないとできない
コマンド実行条件 += "ゲーセンではトイレじゃないと実行できない/"
SIF TEQUIP:ゲームセンター && !TEQUIP:トイレ
	COMRESULT = 0
;触手調教中はダメ
コマンド実行条件 += "触手召喚中は実行できない/"
SIF TEQUIP:触手
	COMRESULT = 0
;性器が露出してないとダメ
コマンド実行条件 += "性器が露出していないと実行できない/"
SIF !性器露出(TARGET)
	COMRESULT = 0
;アイテムを持っていないとダメ
コマンド実行条件 += "アイテム[ローション]が必要"
SIF ITEM:ローション <= 0
	COMRESULT = 0
;ペニス無いとダメ
コマンド実行条件 += "対象に男性器が必要/"
SIF !PENIS(TARGET)
	COMRESULT = 0
;勃起してないとダメ
コマンド実行条件 += @"対象の勃起LV{PALAMLV:3}未満だと実行できない/"
SIF PALAM:勃起 < PALAMLV:3
	COMRESULT = 0

;Ｐ使用中はダメ
コマンド実行条件 += "男性器に道具使用中は実行できない/"
SIF Ｐ使用中(TARGET)
	COMRESULT = 0
;新妻プレイ中はダメ
コマンド実行条件 += "エプロン使用中は実行できない/"
SIF TEQUIP:エプロン
	COMRESULT = 0

IF 条件確定
	COMRESULT = 1
	RETURNF 1
ENDIF
RETURNF COMRESULT

@COM_ローションガーゼ
#DIM DYNAMIC 増加量
;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
IF TEQUIP:触手
	PRINTL 触手亀頭責め
	SELECTCOM = GETNUM(TRAINNAME, "触手亀頭責め")
ELSE
	PRINTL ローションガーゼ
	ITEM:ローション--
ENDIF
CALL KOJO_MESSAGE_COM

;-------------------------------------------------
;射精ゲージチェック（奴隷）
;-------------------------------------------------
増加量 = 100

CALL 射精ゲージ増加, 射精ゲージ計算_Ｐ責め(増加量), TARGET

増加量 = 増加量*(ABL:マゾっ気+1)/5

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
LOSEBASE:体力 += 50
LOSEBASE:気力 += 150

SOURCE:快C = 5000
SOURCE:露出 = 300
SOURCE:不潔 = 500
SOURCE:苦痛 = 1000
SOURCE:逸脱 = 500
SOURCE:反感 = 1000

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:抑圧 || TALENT:抵抗
	SOURCE:鬱屈 += 500

;装飾効果加算
SOURCE:快C += 装飾効果加算量(SOURCE:快C, TARGET, "陰茎")
SOURCE:苦痛 += 装飾効果加算量(SOURCE:快C, TARGET, "陰茎")

;-------------------------------------------------
;絶頂チェック
;-------------------------------------------------
CALL SOURCE_CHECK_CVAB

;-------------------------------------------------
;射精チェック
;-------------------------------------------------
CALL SAMEN_CHECK_T, 0

CALL SAMEN_SHOOT_T, 0, NOWEX:射精

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;触手亀頭責めなら、触手体液がペニスに付く
IF TEQUIP:触手
	SETBIT STAIN:ペニス, 5
ELSE
	STAIN:ペニス |= STAIN:PLAYER:手
	STAIN:PLAYER:手 |= STAIN:ペニス
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
CALL EXPUP_BASE, 2, 3

;潮吹いてれば射精管理経験+
SIF NOWEX:潮噴き
	CALL EXPUP, "射精管理経験", 2

RETURN 1
